plugins {
    id 'com.android.application'
    id 'io.left.rightmesh.rightmesh-plugin' version '1.8.4'
    id 'com.github.kt3k.coveralls'
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.w3engineers.unicef.telemesh"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.support.app_compat
    implementation deps.constraint_layout

    testImplementation deps.junit
    androidTestImplementation deps.atsl.rules
    androidTestImplementation deps.atsl.runner
    androidTestImplementation deps.espresso.core

    api project(':viper')
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}

rightmesh {
    appKey = artifactory_app_key
    username = rightmesh_build_username
    password = rightmesh_build_password
}

preBuild.dependsOn("rightmesh")

repositories {
    maven {
        url "http://artifactory.rightmesh.io/artifactory/maven"
        credentials {
            username rightmesh_build_username
            password rightmesh_build_password
        }
    }
    maven{
        url "https://dl.bintray.com/ethereum/maven/"
    }
    mavenCentral()
    jcenter()

    maven {
        url "https://jitpack.io"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}